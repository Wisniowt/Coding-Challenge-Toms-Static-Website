
resource "aws_cloudfront_origin_access_control" "prod_oac" {
  name                              = "prod-oac"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

resource "aws_cloudfront_distribution" "prod" {

  for_each = aws_s3_bucket.toms_website

  enabled             = true
  default_root_object = "index.html"

  origin {
    domain_name = each.value.bucket_regional_domain_name
    origin_id   = "s3-${each.key}-origin"

    origin_access_control_id = aws_cloudfront_origin_access_control.prod_oac.id
  }

  default_cache_behavior {
    target_origin_id       = "s3-${each.key}-origin"
    viewer_protocol_policy = "redirect-to-https"

    allowed_methods = ["GET", "HEAD"]
    cached_methods  = ["GET", "HEAD"]

    compress = true

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    cloudfront_default_certificate = true
  }
}
